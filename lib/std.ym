def printf(format U8 ptr) I32 = __extern__ __varargs__
def puts(string U8 ptr) I32 = __extern__
def puts(string U8[]) I32 = puts(string.c_str.unsafe_ptr)
def putchar(char I32) I32 = __extern__

def exit(status I32) = __extern__
def assert(condition Bool)
  if !condition
    exit(1)
  end
end

def __builtin_slice_malloc{T}(ptr T ptr, size I64) T ptr = __primitive__(slice_malloc)

struct Slice{T}(pointer T ptr, size I64)
  def :new(::pointer, ::size) end
  def :new(::size)
    ::pointer = __builtin_slice_malloc(::pointer, size) # TODO: this should really be an error, as ::pointer isn't initialized here yet
    let i I64 = 0
    while i < ::size
      ::pointer[i].__builtin_default_init # TODO: this should just be `T()`, which requires implicit default ctors
      i = i + 1
    end
  end

  def size(self) I64 = self::size
  def unsafe_ptr(self) T ptr = self::pointer

  def [](self mut, offset I64) T mut = self::pointer[offset]
  def [](self, offset I64) T = self::pointer[offset]
  def []=(self mut, offset I64, val T) T
    self::pointer[offset] = val
    return val
  end

  def dup(self, extra I64) Self
    let dup_t Self = Self(self::pointer, self::size)
    dup_t::pointer = __builtin_slice_malloc(dup_t::pointer, self::size)
    copy(self::pointer, dup_t::pointer, self::size)
    return dup_t
  end

  def dup_append(self, last T) Self
    let dup_t Self = Self(self::pointer, self::size + 1)
    dup_t::pointer = __builtin_slice_malloc(dup_t::pointer, self::size + 1)
    copy(self::pointer, dup_t::pointer, self::size)
    dup_t[self::size] = last
    return dup_t
  end
end

def []{T}(ptr T ptr, offset I64) T mut = __primitive__(get_at)
def []={T}(ptr T ptr, offset I64, val T) T = __primitive__(set_at)
def __builtin_default_init{T}(obj T mut) = __primitive__(default_init)

# TODO: most of these should be templated
def +(a I32, b I32) I32 = __primitive__(ib_add)
def +(a I64, b I64) I64 = __primitive__(ib_add)
def +(a U8, b U8) U8 = __primitive__(ib_add)
def -(a I32, b I32) I32 = __primitive__(ib_sub)
def -(a I64, b I64) I64 = __primitive__(ib_sub)
def -(a I32) I32 = 0 - a
def -(a U8, b U8) U8 = __primitive__(ib_sub)
def %(a I32, b I32) I32 = __primitive__(ib_srem)
def *(a I32, b I32) I32 = __primitive__(ib_mul)
def //(a I32, b I32) I32 = __primitive__(ib_sdiv)
def &(a U8, b U8) Bool = __primitive__(ib_and)
def ==(a I64, b I64) Bool = __primitive__(ib_icmp_eq)
def ==(a I32, b I32) Bool = __primitive__(ib_icmp_eq)
def ==(a U8, b U8) Bool = __primitive__(ib_icmp_eq)
def !=(a U8, b U8) Bool = __primitive__(ib_icmp_ne)
def >(a I32, b I32) Bool = __primitive__(ib_icmp_sgt)
def >(a I64, b I64) Bool = __primitive__(ib_icmp_sgt)
def <(a I32, b I32) Bool = __primitive__(ib_icmp_slt)
def <(a I64, b I64) Bool = __primitive__(ib_icmp_slt)
def !(a Bool) Bool
  if a
    return false
  else
    return true
  end
end

def copy{T}(source T ptr, dest T ptr, amount I64)
  let count I64 = amount
  while count > 0
    count = count - 1
    dest[count] = source[count]
  end
end

# TODO: type inference should be able to cast I32 to U8, having determined U8 as the replacement for T
def c_str(str U8[]) U8[] = str.dup_append(?\0)

def c_len(ptr U8 ptr) I64
  let terminated Bool = false
  let idx I64 = 0
  while !terminated
    let chr U8 = ptr[idx]
    if chr == ?\0
      terminated = true
    else
      idx = idx + 1
    end
  end
  return idx
end
